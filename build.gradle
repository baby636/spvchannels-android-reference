// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.31"
    ext.hilt_version = '2.33-beta'
    ext.nav_version = '2.3.4'
    ext.coroutines_version = '1.4.1'
    ext.moshi_version = '1.11.0'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.10.2'
}

subprojects {
    repositories {
        google()
        jcenter()
        maven {
            url  "https://dl.bintray.com/terl/lazysodium-maven"
        }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')
            ktlint("0.40.0").userData([android: "true"])
            licenseHeader '''
// Copyright (c) $YEAR Bitcoin Association.
// Distributed under the Open BSV software license, see the accompanying file LICENSE
'''
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            allWarningsAsErrors = true

            jvmTarget = "1.8"
        }
    }
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'spotless/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0755
}

tasks.getByPath(':app:preBuild').dependsOn installGitHook